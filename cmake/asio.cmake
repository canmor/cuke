set(ASIO_URL "https://github.com/chriskohlhoff/asio/archive/refs/tags/asio-1-21-0.zip" CACHE STRING "ASIO URL")
set(ASIO_URL_HASH "MD5=8d21cba2f802967c0e414b5e66673e44" CACHE STRING "ASIO URL HASH")
set(ASIO_GIT_REPOSITORY "https://github.com/chriskohlhoff/asio" CACHE STRING "asio Git Repository URL")
set(ASIO_GIT_TAG "asio-1-21-0" CACHE STRING "asio Git Repository tag")

if (NOT TARGET asio::asio)
    find_package(asio QUIET)
    if (NOT asio_FOUND)
        include(FetchContent)
        if (ASIO_URL STREQUAL "")
            FetchContent_Declare(asio GIT_REPOSITORY ${ASIO_GIT_REPOSITORY} GIT_TAG ${ASIO_GIT_TAG})
        else ()
            FetchContent_Declare(asio URL ${ASIO_URL} URL_HASH ${ASIO_URL_HASH})
        endif ()
        FetchContent_MakeAvailable(asio)
        add_library(asio INTERFACE)
        target_include_directories(asio INTERFACE ${asio_SOURCE_DIR}/asio/include)
        if (WIN32)
            target_link_libraries(asio INTERFACE Ws2_32)
        endif ()
        add_library(asio::asio ALIAS asio)
    endif ()
endif ()

